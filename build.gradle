import java.text.SimpleDateFormat

plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'java'
}

group = "com.shweit.expendablebackpacks"
version = "1.1.2"

static def getTime() {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    return sdf.format(new Date()).toString()
}

// Optional: Override version with -Pver=x.y.z
if (hasProperty('ver')) {
    String shortVersion
    if (ver.charAt(0) == "v") {
        shortVersion = ver.substring(1)
    } else {
        shortVersion = ver
    }

    int rcIdx
    if ((rcIdx = shortVersion.indexOf("-RC-")) != -1) {
        version = shortVersion.substring(0, rcIdx) + "-SNAPSHOT"
    } else {
        version = shortVersion
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeModule("dev.folia", "folia-api")
            includeModule("io.papermc", "paperlib")
            includeModule("net.md-5", "bungeecord-chat")
            includeGroup("io.papermc.adventure")
        }
    }

    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net'
        content {
            includeModule("com.mojang", "brigadier")
        }
    }

    mavenCentral()
}

dependencies {
    compileOnly 'dev.folia:folia-api:1.21.11-R0.1-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.8'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}


processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: rootProject.group.toString() )
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'io.papermc.lib', 'shadow.io.papermc.paperlib'
    relocate 'org.bstats', 'com.shweit.expendablebackpacks.bstats'
    minimize()
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

tasks.register('printProjectName') {
    doLast {
        println rootProject.name
    }
}

tasks.register('release') {
    dependsOn build

    doLast {
        if (!version.endsWith("-SNAPSHOT")) {
            // Rename final JAR to trim off version information
            shadowJar.archiveFile.get().getAsFile()
                    .renameTo(layout.buildDirectory.get().toString() + File.separator + 'libs' + File.separator
                            + rootProject.name + '.jar')
        }
    }
}
